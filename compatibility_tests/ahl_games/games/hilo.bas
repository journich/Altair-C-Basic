10 PRINT TAB(34);"HI LO"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
30 PRINT:PRINT:PRINT
100 PRINT "THIS IS THE GAME OF HI LO."
110 PRINT
120 PRINT "YOU WILL HAVE 6 TRIES TO GUESS THE AMOUNT OF MONEY IN THE"
130 PRINT "JACKPOT, WHICH IS BETWEEN 1 AND 100 DOLLARS.  IF YOU GUESS"
140 PRINT "THE AMOUNT, YOU WIN ALL THE MONEY IN THE JACKPOT!"
150 PRINT "THEN YOU GET ANOTHER CHANCE TO WIN MORE MONEY."
160 PRINT "HOWEVER, IF YOU DO NOT GUESS THE AMOUNT, THE GAME ENDS."
170 PRINT
200 Y=INT(100*RND(1)+1)
210 T=0
215 PRINT "I'M THINKING OF A NUMBER BETWEEN 1 AND 100."
220 T=T+1
230 PRINT "YOUR GUESS";
240 INPUT G
250 IF G=Y THEN 300
260 IF T>=6 THEN 350
270 IF G<Y THEN PRINT "YOUR GUESS IS TOO LOW."
275 IF G>Y THEN PRINT "YOUR GUESS IS TOO HIGH."
280 GOTO 220
300 PRINT "GOT IT!!!!!!!!"
310 PRINT "YOU WIN";Y;"DOLLARS."
315 W=W+Y
320 PRINT "YOUR TOTAL WINNINGS ARE NOW";W;"DOLLARS."
330 PRINT:PRINT "PLAY AGAIN (YES OR NO)";
340 INPUT A$:IF LEFT$(A$,1)="Y" THEN 200
345 GOTO 400
350 PRINT "YOU BLEW IT...TOO BAD...THE NUMBER WAS";Y
360 PRINT "YOUR TOTAL WINNINGS ARE";W;"DOLLARS."
400 PRINT:PRINT "SO LONG.  HOPE YOU ENJOYED YOURSELF!!!"
999 END
